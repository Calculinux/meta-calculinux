require conf/distro/poky.conf
require conf/distro/include/gcsections.inc

# Ensure access to core recipe classes such as compress_doc
BBPATH:append = ":${COREBASE}/meta/classes-recipe"

# Required by BB_SIGNATURE_HANDLER = "OEEquivHash"
SSTATE_HASHEQUIV_METHOD ?= "oe.sstatesig.OEOuthashBasic"

DISTRO = "calculinux-distro"
DISTRO_NAME = "Calculinux PicoCalc Edition"
DISTROOVERRIDES = "poky:calculinux-distro"
TCLIBC = "musl"

# DISTRO_VERSION - Override Poky's version with Calculinux versioning
# We use = assignment which overrides Poky's = assignment (parsed earlier).
# Local builds get a sensible default with git hash.
# CI builds create kas-ci-override.yaml which adds local_conf_header entries
# that override this value (parsed last, so they win).
DISTRO_VERSION = "1.0.0-dev+${@bb.process.run('git -C ${TOPDIR}/../meta-calculinux rev-parse --short HEAD 2>/dev/null || echo unknown', shell=True)[0].strip()}"
# CI version patterns:
# - Tagged releases: DISTRO_VERSION = "v1.0.0-alpha4" (from git tag)
# - Main branch: DISTRO_VERSION = "1.0.0-continuous+<git-hash>"
# - Develop branch: DISTRO_VERSION = "1.0.0-develop+<git-hash>"

# We can use packagegroup-core-boot, but in the future we may need a new packagegroup-core-tiny
#POKY_DEFAULT_EXTRA_RDEPENDS += "packagegroup-core-boot"
# Drop kernel-module-af-packet from RRECOMMENDS
POKY_DEFAULT_EXTRA_RRECOMMENDS = ""

DISTRO_FEATURES += "ipv4 usbhost usbgadget wifi overlayfs rauc nls read-only-rootfs keyboard doc"

# Enable automatic compression of manpages and info pages in all recipes
INHERIT_DISTRO:append = " compress_doc"

# Enable package management with opkg
PACKAGE_CLASSES = "package_ipk"

# Generate package index for opkg feeds
INHERIT += "package_ipk"

# Package feed configuration
# Feed structure:
# - ipk/{codename}/continuous/ - main branch continuous builds
# - ipk/{codename}/release/    - tagged releases  
# - ipk/develop/continuous/    - develop branch builds
#
# CALCULINUX_FEED_SUBFOLDER can be overridden:
# - In local.conf for custom builds
# - In kas files for different build types
# - Defaults to "continuous" for normal builds
CALCULINUX_FEED_SUBFOLDER ??= "continuous"

# For develop branch builds, override DISTRO_CODENAME to "develop"
# This can be done in kas-{machine}-develop.yaml or via local.conf
# DISTRO_CODENAME = "develop"  # Uncomment for develop builds

PACKAGE_FEED_URIS = "https://opkg.calculinux.org/"
PACKAGE_FEED_BASE_PATHS = "ipk/${DISTRO_CODENAME}/${CALCULINUX_FEED_SUBFOLDER}"

DISTRO_FEATURES:class-native = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"
DISTRO_FEATURES:class-nativesdk = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"

# We try systemd to see how well it runs
DISTRO_FEATURES:append = " systemd usrmerge"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"
# Disabling pulseaudio and sysvinit
DISTRO_FEATURES_BACKFILL_CONSIDERED += "pulseaudio sysvinit gobject-introspection-data ldconfig"
INIT_MANAGER ?= "systemd"
# POKY_INIT_MANAGER:poky-tiny = "mdev-busybox"

IMAGE_FSTYPES = "wic.gz tar.gz"

# Drop v86d from qemu dependency list (we support serial)
# Drop grub from meta-intel BSPs
# FIXME: A different mechanism is needed here. We could define -tiny
#        variants of all compatible machines, but that leads to a lot
#        more machine configs to maintain long term.
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ""

# The mtrace script included by eglibc is a perl script. This means the system
# will build perl in case this package is installed. Since we don't care about
# this script for the purposes of tiny, remove the dependency from here.
RDEPENDS:${PN}-mtrace:pn-eglibc = ""

# Disable python usage in opkg-utils since it won't build with tiny config
PACKAGECONFIG:remove:pn-opkg-utils = "python"

# Disable GTK GUI and sound for vim so we don't pull in an entire desktop stack
PACKAGECONFIG:remove:pn-vim = "gtkgui sound"

# Enable wifi for connman
# PACKAGECONFIG:pn-connman = "wispr client nftables wifi wpa-supplicant iwd"

# Disable meta-retro attempt to create user/groups 
RETRO_USERADD_COMMAND = ""

# SDK Configuration - Include kernel headers and dev packages for all image contents
TOOLCHAIN_TARGET_TASK += "kernel-devsrc"
TOOLCHAIN_TARGET_TASK += "packagegroup-core-standalone-sdk-target"
# Limit linux-firmware to only USB WiFi adapters
# Exclude all unwanted firmware packages to prevent bloat
PACKAGE_EXCLUDE += " \
    linux-firmware-amdgpu \
    linux-firmware-i915 \
    linux-firmware-nvidia \
    linux-firmware-radeon \
    linux-firmware-xe \
    linux-firmware-netronome \
    linux-firmware-qcom \
    linux-firmware-mellanox \
    linux-firmware-mrvl-prestera \
    linux-firmware-mrvl \
    linux-firmware-liquidio \
    linux-firmware-ath10k \
    linux-firmware-ath11k \
    linux-firmware-ath12k \
    linux-firmware-dpaa2 \
    linux-firmware-qed \
    linux-firmware-intel \
    linux-firmware-ti \
    linux-firmware-cirrus \
    linux-firmware-cxgb4 \
"
